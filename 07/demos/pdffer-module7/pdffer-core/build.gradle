plugins {
	id 'org.springframework.boot' version '2.5.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java-library'
	id 'maven-publish'
	id 'signing'
}

group = 'org.nekosoft.pdffer'
version = '1.0-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

java {
	withJavadocJar()
	withSourcesJar()
	registerFeature('web') {
		usingSourceSet(sourceSets.main)
	}
	registerFeature('email') {
		usingSourceSet(sourceSets.main)
	}
	registerFeature('shell') {
		usingSourceSet(sourceSets.main)
	}
}

bootJar {
  enabled = false
}

jar {
  enabled = true
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	api(project(':pdffer-template'))
	webApi 'org.springframework.boot:spring-boot-starter-web'
	emailApi 'org.springframework.boot:spring-boot-starter-mail'
	shellApi 'com.fasterxml.jackson.core:jackson-databind'
	shellApi 'org.springframework.shell:spring-shell-starter:2.0.1.RELEASE'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
}

test {
	useJUnitPlatform()
}

ext.isReleaseVersion = !version.endsWith("-SNAPSHOT")

publishing {
	repositories {
		maven {
			def releaseRepo = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotRepo = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
			url = isReleaseVersion ? releaseRepo : snapshotRepo
			credentials {
				username = project.hasProperty('ossrhUsername') ? project.getProperties().get('ossrhUsername') : System.getenv('OSSRH_USERNAME')
				password = project.hasProperty('ossrhPassword') ? project.getProperties().get('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
			}
		}
	}

	publications {
		mavenJava(MavenPublication) {
			pom {
				groupId = project.group
				name = "Spring Boot PDFfer Core"
				description = 'A Spring Boot library to quickly and easily add PDF generation functionality to any Spring Boot application'
				url = 'https://pdffer.nekosoft.org/'
				from components.java
				artifact (jar) {
					classifier = null
				}
				licenses {
					license {
						name = 'The Apache License, Version 2.0'
						url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}
				scm {
					connection = 'scm:git:git@github.com:PDFfer/pdffer-core.git'
					developerConnection = 'scm:git:git@github.com:PDFfer/pdffer-core.git'
					url = 'https://github.com/PDFfer/pdffer-core'
				}
			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
	onlyIf { isReleaseVersion }
}
